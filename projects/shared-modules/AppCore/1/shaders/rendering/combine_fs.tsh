$input v_texcoord0

#include <torque6.tsh>
#include <lighting.tsh>

SAMPLER2D(Texture0, 0); // Albedo
SAMPLER2D(Texture1, 1); // Material Info
SAMPLER2D(Texture2, 2); // Diffuse Light Buffer
SAMPLER2D(Texture3, 3); // Specular Light Buffer

void main()
{
    Surface surface;

    // Albedo
    surface.albedo = decodeRGBE8(texture2D(Texture0, v_texcoord0));

    // Material Parameters
    vec4  matInfo       = texture2D(Texture1, v_texcoord0);
    surface.metallic    = matInfo.r;
    surface.roughness   = matInfo.g;
    surface.emissive    = matInfo.a;

    // Surface Colors
    vec3 diffuseColor   = surface.albedo - (surface.albedo * surface.metallic);
    vec3 specularColor  = mix(vec3_splat(0.04), surface.albedo, surface.metallic);

    // Lighting
    vec4 diffuseLight   = texture2D(Texture2, v_texcoord0);
    vec4 specularLight  = texture2D(Texture3, v_texcoord0);
    vec3 light          = (diffuseColor * diffuseLight.rgb) + (specularColor * specularLight.rgb);

    // Output with emissive support
    gl_FragColor = encodeRGBE8(light * (1.0 - surface.emissive) + (surface.albedo * surface.emissive));
}
