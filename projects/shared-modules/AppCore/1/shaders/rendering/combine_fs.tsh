$input v_texcoord0

#include <torque6.tsh>
#include <lighting.tsh>

SAMPLER2D(Texture0, 0); // Albedo
SAMPLER2D(Texture1, 1); // Material Info
SAMPLER2D(Texture2, 2); // Direct Light Buffer
SAMPLER2D(Texture3, 3); // Indirect Light Buffer

void main()
{
    Surface surface;

    // Albedo
    surface.albedo = decodeRGBE8(texture2D(Texture0, v_texcoord0));

    // Material Parameters
    vec4  matInfo       = texture2D(Texture1, v_texcoord0);
    surface.metallic    = matInfo.r;
    surface.roughness   = matInfo.g;
    surface.emissive    = matInfo.a;

    // Direct Lighting
    vec3 diffuseColor   = surface.albedo - (surface.albedo * surface.metallic);
    vec3 lightColor     = texture2D(Texture2, v_texcoord0).rgb;
    vec3 directLight    = diffuseColor * lightColor;

    // Indirect Lighting
    vec3 indirectLight = texture2D(Texture3, v_texcoord0).rgb;

    // Output with emissive support
    gl_FragColor = encodeRGBE8((directLight + indirectLight) * (1.0 - surface.emissive) + (surface.albedo * surface.emissive));
}
